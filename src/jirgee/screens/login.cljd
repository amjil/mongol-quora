(ns jirgee.screens.login
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter.alpha2 :as f]
   [jirgee.common.theme :as theme]))

(defn auth-form-field [controller hint-text]
  (m/TextFormField
   .controller controller
   .decoration 
   (m/InputDecoration
    .focusedBorder
    (m/OutlineInputBorder
     .borderRadius (.circular m/BorderRadius 5)
     .borderSide (m/BorderSide .color theme/blue-color .width 3))
    .enabledBorder (m/OutlineInputBorder
                    .borderRadius (.circular m/BorderRadius 5)
                    .borderSide 
                    (m/BorderSide .color theme/grey-color))
    .contentPadding (.all m/EdgeInsets 22)
    .hintText hint-text
    .hintStyle (m/TextStyle .fontSize 18))))

(defn rounded-button [on-tap label bg-color text-color]
  (m/InkWell
   .onTap on-tap
   .child (m/Chip
           .label
           (mgl/MongolText label
                           .style (m/TextStyle
                                   .color (if (nil? text-color) theme/background-color text-color)
                                   .fontSize 16))
           .backgroundColor (if (nil? bg-color) theme/white-color bg-color)
           .labelPadding (.symmetric m/EdgeInsets .horizontal 5 .vertical 20))))

(defn ^m/Widget login-screen [ctx]
  (f/widget
   :managed [email-controller (m/TextEditingController)
             password-controller (m/TextEditingController)]
   (m/Scaffold)
   .body
   (m/SafeArea)
   (m/Center)
   (m/Padding
    .padding (.symmetric m/EdgeInsets .horizontal 20))
   (m/Column
    .children 
    [(auth-form-field email-controller "Email")
     (m/SizedBox .height 25)
     (auth-form-field password-controller "Password")
     (m/SizedBox .height 40)
     (m/Align
      .alignment m.Alignment/topRight
      .child (rounded-button #(dart:core/print "hello")
                             "Done" nil nil))
     (m/SizedBox .height 40)])))
